plugins {
    id "nebula.lint" version "20.2.0"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'

java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
}

def mainClassName = "se.kb.libris.foliointegration.Server"

repositories {
    mavenCentral()
}

dependencies {
    // Servlet
    api "jakarta.servlet:jakarta.servlet-api:4.0.3"
    implementation "org.eclipse.jetty:jetty-server:12.0.6"
    implementation "org.eclipse.jetty.ee8:jetty-ee8-servlet:12.0.6"
}

shadowJar {
    archiveClassifier = null // removes `-all` in the filename of the created .jar
    mergeServiceFiles()
}

jar {
    dependsOn(shadowJar)

    manifest {
        attributes "Main-Class": mainClassName
    }

    archiveClassifier = "nonfat"
    enabled = false
}

task(appRun, dependsOn: "classes", type: JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = mainClassName
    minHeapSize = "1g"
    maxHeapSize = "4g"
    systemProperties(
            'http.port': System.getProperty("http.port", "8080")
    )
    args(System.getProperty("args", "").split() as String[])

    debugOptions {
        enabled = true
        port = 5007
        server = true
        suspend = false
    }
}
